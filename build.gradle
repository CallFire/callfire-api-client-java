group 'com.callfire'

apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = 1.7

def clientBaseName = 'callfire-api-client'
def clientVersion = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    baseName = clientBaseName
    version = clientVersion
    // TODO vmikhailov configure manifest file
    manifest {
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = clientVersion + '-sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = clientVersion + '-javadoc'
    from javadoc.destinationDir
}

task clientFatJar(type: Jar, dependsOn: jar) {
    baseName = clientBaseName
    version = clientVersion + '-all'
    manifest {
    }
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives clientFatJar
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'

    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile 'org.apache.httpcomponents:httpmime:4.5'
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile('junit:junit:4.12') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

idea {
    module {
        downloadJavadoc = true
    }
}